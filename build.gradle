buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        mavenCentral()
        maven { url = 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.0.0'
        classpath 'net.kyori:blossom:1.3.0'
    }
}
apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'net.kyori.blossom'


group = 'net.buycraft' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
version = '1.0.0'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenLocal()
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

blossom{
    replaceTokenIn('net/buycraft/plugin/forge/BuycraftPlugin.java')
    replaceToken('${pluginVersion}', project.version)
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    //mappings channel: 'snapshot', version: '20171003-1.12'
    mappings channel: 'snapshot', version: '20171003-1.12'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2855'
    shadow 'com.github.tebexio.BuycraftX:buycraftx-plugin-shared:4c2cdb00d1'
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'okhttp3', 'net.buycraft.plugin.internal.okhttp3'
    relocate 'okio', 'net.buycraft.plugin.internal.okio'
    relocate 'retrofit2', 'net.buycraft.plugin.internal.retrofit2'
    relocate 'com.fasterxml.jackson', 'net.buycraft.plugin.internal.jackson'
	archiveBaseName.set('buycraftx-forge')
	archiveClassifier.set('')
}

afterEvaluate {
    reobf {
        shadowJar {
            mappings = project.tasks.getByName('createMcpToSrg').output
        } // Reobfuscate the shadowed JAR
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "BuycraftX",
                "Specification-Vendor"    : "Tebex",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "Tebex",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}